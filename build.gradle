plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1' apply false
	id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
	group = 'site.aiion'
	version = '0.0.1-SNAPSHOT'
	
	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}
	
	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}
	
	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:2024.0.0"
		}
		dependencies {
			// QueryDSL
			dependency 'com.querydsl:querydsl-jpa:5.0.0'
			dependency 'com.querydsl:querydsl-apt:5.0.0'
			
			// Jakarta
			dependency 'jakarta.servlet:jakarta.servlet-api:6.0.0'
			dependency 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.1'
			
			// Selenium
			dependency 'org.seleniumhq.selenium:selenium-java:4.19.1'
			dependency 'org.seleniumhq.selenium:selenium-chrome-driver:4.19.1'
			dependency 'io.github.bonigarcia:webdrivermanager:5.3.0'
			
			// JSoup
			dependency 'org.jsoup:jsoup:1.15.3'
			
			// Database
			dependency 'org.postgresql:postgresql:42.7.3'
			
			// Swagger
			dependency 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
			
			// Kotlin
			dependency 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
			
			// JSON
			dependency 'com.googlecode.json-simple:json-simple:1.1.1'
			dependency 'org.json:json:20211205'
			
			// AWS SDK
			dependency 'com.amazonaws:aws-java-sdk-s3:1.12.300'
		}
	}
	
	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}
	
	tasks.named('test') {
		useJUnitPlatform()
	}
}

// Service 모듈에 공통 의존성 적용
configure(subprojects.findAll { it.path.startsWith(':service:') && it.path.contains('-service') }) {
	dependencies {
		// QueryDSL
		implementation('com.querydsl:querydsl-jpa:5.0.0') {
			classifier = 'jakarta'
		}
		annotationProcessor('com.querydsl:querydsl-apt:5.0.0') {
			classifier = 'jakarta'
		}
		annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
		annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
		
		// Jakarta Servlet
		implementation 'jakarta.servlet:jakarta.servlet-api'
		
		// Spring Cloud
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
		implementation 'org.springframework.cloud:spring-cloud-starter-config'
		
		// Spring Boot 스타터
		implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-aop'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
		
		// Selenium
		implementation 'org.seleniumhq.selenium:selenium-java'
		implementation 'org.seleniumhq.selenium:selenium-chrome-driver'
		implementation 'io.github.bonigarcia:webdrivermanager'
		
		// JSoup
		implementation 'org.jsoup:jsoup'
		
		// Spring Boot DevTools
		developmentOnly 'org.springframework.boot:spring-boot-devtools'
		
		// Database (PostgreSQL - docker-compose에서 사용)
		runtimeOnly 'org.postgresql:postgresql'
		
		// Swagger
		implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui'
		
		// Kotlin
		implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
		
		// JSON 파싱
		implementation 'com.googlecode.json-simple:json-simple'
		implementation 'org.json:json'
		
		// AWS SDK S3
		implementation 'com.amazonaws:aws-java-sdk-s3'
		implementation 'jakarta.xml.bind:jakarta.xml.bind-api'
	}
}
